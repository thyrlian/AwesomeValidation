apply plugin: "com.android.library"
apply plugin: "com.getkeepsafe.dexcount"
apply from: "gradle/script/coverage.gradle"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        defaultPublishConfig "release"
        consumerProguardFiles "proguard-rules.pro"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    sourceSets {
        main {
            java.srcDirs = ["src/main/java"]
            resources.srcDirs = ["src/main/res"]
        }
        test {
            java.srcDirs = ["src/test/java"]
            resources.srcDirs = ["src/test/res"]
        }
        androidTest.setRoot("test")
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        abortOnError false
    }
}

ext {
    guavaVersion = "27.0.1-android"
    junitVersion = "4.12"
    mockitoVersion = "2.21.0"
    easymockVersion = "3.6"
    powermockVersion = "2.0.0-beta.5"
}

dependencies {
    implementation 'com.google.android.material:material:1.1.0-alpha03'
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"
    api("com.google.guava:guava:${guavaVersion}") {
        exclude module: "jsr305"
    }
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.easymock:easymock:${easymockVersion}"
    testImplementation "org.powermock:powermock-module-junit4:${powermockVersion}"
    testImplementation "org.powermock:powermock-api-mockito2:${powermockVersion}"
    testImplementation "org.powermock:powermock-api-easymock:${powermockVersion}"
}

configurations.all {
    resolutionStrategy {
        force "junit:junit:${junitVersion}"
    }
}

apply from: "$rootProject.projectDir/publish.gradle"

apply plugin: "com.android.library"
apply plugin: "com.getkeepsafe.dexcount"
apply from: "gradle/script/coverage.gradle"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        defaultPublishConfig "release"
        publishNonDefault true
        consumerProguardFiles "proguard-rules.pro"
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    sourceSets {
        main {
            java.srcDirs = ["src/main/java"]
            resources.srcDirs = ["src/main/res"]
        }
        test {
            java.srcDirs = ["src/test/java"]
            resources.srcDirs = ["src/test/res"]
        }
        androidTest.setRoot("test")
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    lintOptions {
        abortOnError false
    }
}

ext {
    guavaVersion = "23.6-android"
    junitVersion = "4.12"
    mockitoVersion = "2.9.0"
    easymockVersion = "3.5.1"
    powermockVersion = "1.7.3"
}

dependencies {
    implementation "com.android.support:design:${rootProject.ext.androidSupportLibVersion}"
    implementation "com.android.support.constraint:constraint-layout:1.0.2"
    api("com.google.guava:guava:${guavaVersion}") {
        exclude module: "jsr305"
    }
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.easymock:easymock:${easymockVersion}"
    testImplementation "org.powermock:powermock-module-junit4:${powermockVersion}"
    testImplementation "org.powermock:powermock-api-mockito2:${powermockVersion}"
    testImplementation "org.powermock:powermock-api-easymock:${powermockVersion}"
}

configurations.all {
    resolutionStrategy {
        force "junit:junit:${junitVersion}"
    }
}

apply from: "$rootProject.projectDir/publish.gradle"
